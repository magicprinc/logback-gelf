plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.5.0'
    id("net.ltgt.errorprone")  version "latest.release" // https://errorprone.info/
}

group = "de.siegmar"
version = "4.5"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:latest.release")// annotation processor

    api 'ch.qos.logback:logback-classic:1.2.12'

    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    testImplementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // = compileJava.options.encoding = "UTF-8"
    options.compilerArgs.addAll(['-Xlint:all,-serial', '-parameters'])
    options.deprecation = true

    options.errorprone {
        enabled = true  // <<<!!! You can disable Error-Prone here!
        disableWarningsInGeneratedCode = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "logback-gelf"
            from components.java

            pom {
                name = 'Logback GELF'
                description = 'Logback appender for sending GELF messages with zero additional dependencies.'
                url = 'https://github.com/osiegmar/logback-gelf'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                scm {
                    url = 'https://github.com/osiegmar/logback-gelf'
                    connection = 'scm:git:https://github.com/osiegmar/logback-gelf.git'
                }
                developers {
                    developer {
                        id = 'osiegmar'
                        name = 'Oliver Siegmar'
                        email = 'oliver@siegmar.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}